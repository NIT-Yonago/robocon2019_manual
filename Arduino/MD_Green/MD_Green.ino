//Morita Daiki 2019 @NIT.yonago
//2019/09/25
//Arduino Nano (old etc..)
//Black Motor Driver &ã‚¤ãƒ³ãƒã‚­ã‚¨ã‚¢ãƒ¼
// #include <serial_communication_v0.h>
#include "PwmMotor.h"
#include "gyro_integral.h"

const int air_pin[3] = {A1, A2, A3}; //ä¸Šä¸‹ï¼Œå‰å¾Œï¼Œã¤ã‹ã‚€
boolean air_state[3];                //ç¾åœ¨ã®ã‚¨ã‚¢çŠ¶æ…‹
int speed[3];                        //ãƒ¢ãƒ¼ã‚¿é€Ÿåº¦

unsigned long serial_tim_last = 0, gyro_tim_last = 0; //ãƒ«ãƒ¼ãƒ—ç›£è¦–ç”¨æ™‚é–“ä¿æŒå¤‰æ•°
boolean sta13 = false, enkaige = false;               //LEDçŠ¶æ…‹ï¼Œå®´ä¼šèŠ¸ï¼ˆå›è»¢è£œæ­£ï¼‰ãƒ•ãƒ©ã‚°
float want_deg = 0, rot = 0.0, error_angle = 0.0;     //ç›®æ¨™è§’ï¼Œå›è»¢æˆåˆ†,è§’åº¦èª¤å·®
int vx, vy, vrot, vrot_old;                           //Xé€Ÿåº¦ï¼ŒYé€Ÿåº¦ï¼Œæ—‹å›é€Ÿåº¦ï¼Œéå»æ—‹å›é€Ÿåº¦

PwmMotor motor[3] = {
    //cw,ccw,pwm
    PwmMotor(4, 2, 3),
    PwmMotor(7, 5, 6),
    PwmMotor(A0, 8, 9),
};
gyro_integral gyro_1; //ã‚¸ãƒ£ã‚¤ãƒ­ã‚»ãƒ³ã‚µã®åå‰

void setup() //åˆæœŸè¨­å®š
{
    for (int i = 0; i < 3; i++)
        pinMode(air_pin[i], OUTPUT);                //å‡ºåŠ›ã«è¨­å®š
    pinMode(13, OUTPUT);                            //LED Pin
    Serial.begin(115200);                           //hunging_systemã¨ã®é€šä¿¡é€Ÿåº¦ã‚’å¤‰æ›´
    Serial.println("tim,want,jsx,d/s,fild/s,angl"); //index print
    gyro_1.init(500);                               //ã‚¸ãƒ£ã‚¤ãƒ­åˆæœŸã‚ªãƒ•ã‚»ãƒƒãƒˆè¨ˆç®—ï¼ˆï¼•ï¼ï¼å›ï¼‰
}

void loop() //ãƒ¡ã‚¤ãƒ³ãƒ«ãƒ¼ãƒ—
{
    if ((millis() - gyro_tim_last) > 4) //4msä»¥ä¸ŠçµŒéã—ãŸã‚‰ç©åˆ†
    {
        gyro_1.integral();        //ç©åˆ†
        gyro_tim_last = millis(); //æ™‚é–“æ›´æ–°
        Serial.print(vx);
        Serial.print(",");
        Serial.print(vy);
        Serial.print(",");
        Serial.print(vrot);
        Serial.print(",");
        Serial.print(want_deg);
        Serial.print(",");
        Serial.println(gyro_1.robot_angle);
    }

    if ((millis() - serial_tim_last) > 1000) //æ“ç¸¦ãŒé•·ã‚‰ããªã„ã¨ããƒªã‚»ãƒƒãƒˆ
        reset();

    if (Serial.available() > 0) //ã‚·ãƒªã‚¢ãƒ«å—ä¿¡æ™‚
    {
        if (Serial.read() == 0xff) //ãƒ˜ãƒƒãƒ€ãªã‚‰
        {
            digitalWrite(13, sta13); //LEDã¡ã‹ã¡ã‹
            sta13 = !sta13;          //LEDã¡ã‹ã¡ã‹

            uint8_t read_data[4];           //å—ä¿¡ãƒ‡ãƒ¼ã‚¿
            Serial.readBytes(read_data, 4); //å—ä¿¡
            serial_tim_last = millis();     //ã‚·ãƒªã‚¢ãƒ«ã®å—ä¿¡æ™‚é–“æ›´æ–°

            vx = read_data[0] - 31;      //-31~32
            vy = read_data[1] - 31;      //-31~32
            vrot = -(read_data[2] - 15); //-15~16
            air_move(read_data[3]);
        }
    }

    error_angle = want_deg - gyro_1.robot_angle;   //èª¤å·®
    rot = constrain(error_angle * 4.0, -255, 255); //èª¤å·®è£œæ­£å‡ºåŠ›
    if (vrot == 0)                                 //JSãŒåŸç‚¹ã«æˆ»ã£ãŸæ™‚
    {
        if (vrot_old != 0)                 //ã‚‚ã¨ã‚‚ã¨æ—‹å›å¸Œæœ›ãªã‚‰
            want_deg = gyro_1.robot_angle; //ãã‚Œã‚’ç›®æ¨™è§’åº¦ã«ã™ã‚‹
    }
    else                 //ãã®ä»–
        rot = vrot * 15; //JSãŒæ“ä½œä¸­ã¯æ—‹å›é€Ÿåº¦ã‚’æŒ‡å®š
    if ((abs(vx) + abs(vy)) < 5 && abs(vrot) < 2)
    {
        if (!enkaige)
            rot = 0;
    }
    omni(vx, vy, rot); //é€Ÿåº¦å‡ºåŠ›

    vrot_old = vrot; //ã‹ã“JSæ›´æ–°
}

void air_move(uint8_t air_cmd) //0:ä¸Šä¸‹,1:å‰å¾Œ,2:çˆª
{
    if ((air_cmd >> 3) & 1) //ğŸ‘†
        air_state[0] = true;
    else if ((air_cmd >> 4) & 1) //ğŸ‘‡
        air_state[0] = false;

    if ((air_cmd >> 1) & 1) //å‰
        air_state[1] = true;
    else if ((air_cmd >> 2) & 1) //å¾Œ
        air_state[1] = false;

    if ((air_cmd >> 0) & 1)           //çˆªé–‹é–‰
        air_state[2] = !air_state[2]; //åˆ‡ã‚Šæ›¿ãˆå¯èƒ½

    if ((air_cmd >> 5) & 1) //x&HOME
    {
        reset();          //å…¨åˆæœŸåŒ–
        gyro_1.init(500); //ã‚¸ãƒ£ã‚¤ãƒ­åˆæœŸåŒ–
    }
    if ((air_cmd >> 6) & 1) //y
        enkaige = true;
    else
        enkaige = false;

    for (int i = 0; i < 3; i++)
        digitalWrite(air_pin[i], air_state[i]);
}

void omni(int vx, int vy, float vtheta)
{
    float v = sqrt(vx * vx + vy * vy) * 4.0; //é€Ÿåº¦ã¯åŠå¾„ï¼Šï¼”
    float theta = atan2(vy, vx);             //æ–¹å‘ã¯atan^-1(y/x)
    float R[3] = {1.0, 1.0, 1.0};            //ãƒ­ãƒœãƒƒãƒˆã®ã‚¿ã‚¤ãƒ¤é…ç½®ï¼ˆä¸­å¿ƒã«è¿‘ã„ã¨å›è»¢é€Ÿåº¦ä½ä¸‹ï¼‰
    for (int i = 0; i < 3; i++)              //å„ãƒ¢ãƒ¼ã‚¿ã«å¯¾ã—ã¦ï¼
        speed[i] = -v * cos(theta + PI - PI * 2.0 / 3.0 * i) - vtheta * R[i];
    int max = 255;              //æœ€å¤§å€¤ï¼ˆä»®ï¼‰
    for (int i = 0; i < 3; i++) //æœ€å¤§å€¤æ›´æ–°
        max = max(abs(speed[i]), max);
    for (int i = 0; i < 3; i++) //å„ãƒ¢ãƒ¼ã‚¿ã«å¯¾ã—ã¦ï¼
    {
        speed[i] = map(speed[i], -max, max, -255, 255); //æ›´æ–°ã—ãŸæœ€å¤§å€¤ã§æ­£è¦åŒ–
        motor[i].speed(speed[i]);                       //ãƒ¢ãƒ¼ã‚¿ãƒ¼å›è»¢
    }
}

void reset() //ãƒªã‚»ãƒƒãƒˆ
{
    vx = 0;                   //é€Ÿåº¦0
    vy = 0;                   //é€Ÿåº¦0
    vrot = 0;                 //æ—‹å›ï¼
    vrot_old = 0;             //éå»æ—‹å›ï¼
    omni(0, 0, 0);            //ã‚¹ãƒ”ãƒ¼ãƒ‰ã‚ªãƒ•
    want_deg = 0.0;           //ç›®æ¨™ãƒªã‚»ãƒƒãƒˆ
    gyro_1.robot_angle = 0.0; //ãƒ­ãƒœãƒƒãƒˆè§’åº¦ãƒªã‚»ãƒƒãƒˆ
}
